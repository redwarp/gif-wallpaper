plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id "com.diffplug.spotless" version "6.7.2"
    id "se.ascp.gradle.gradle-versions-filter" version "0.1.16"
}

android {
    compileSdkVersion 32

    defaultConfig {
        applicationId "net.redwarp.gifwallpaper"
        minSdkVersion 21
        targetSdkVersion 32
        versionCode 67
        versionName "1.13.4"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        resConfigs "en", "fr", "ru", "de", "es", "it", "zh"
    }

    buildFeatures {
        compose true
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = "11"
        freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
    }

    sourceSets {
        main {
            resources {
                srcDirs 'src/main/resources', "$rootDir/docs/"
            }
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    composeOptions {
        kotlinCompilerExtensionVersion '1.1.1'
    }
}

spotless {
    kotlin {
        target project.fileTree(project.rootDir) {
            include '**/net/redwarp/gifwallpaper/**/*.kt'
        }

        licenseHeaderFile 'license_header.kt'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'com.google.android.material:material:1.6.1'
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.2'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.2'
    implementation 'androidx.lifecycle:lifecycle-service:2.4.1'
    implementation 'androidx.palette:palette-ktx:1.0.0'
    implementation "io.noties.markwon:core:4.6.2"
    implementation "app.redwarp.gif:decoder:1.2.0"
    implementation "app.redwarp.gif:android-drawable:1.2.0"
    implementation "androidx.datastore:datastore-preferences:1.0.0"

    // Integration with activities
    implementation 'androidx.activity:activity-compose:1.4.0'
    // Compose Material Design
    implementation 'androidx.compose.material:material:1.1.1'
    // Animations
    implementation 'androidx.compose.animation:animation:1.1.1'
    // Tooling support (Previews, etc.)
    implementation 'androidx.compose.ui:ui-tooling:1.1.1'
    implementation "androidx.compose.ui:ui-tooling-preview:1.1.1"
    implementation "androidx.navigation:navigation-compose:2.4.2"

    implementation "com.google.accompanist:accompanist-flowlayout:0.23.1"

    // Integration with ViewModels
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.4.1'
    // UI Tests
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4:1.1.1'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.robolectric:robolectric:4.8.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

dependencyUpdates {
    rejectVersionIf {
        isNonStable(it.candidate.version)
    }
}
