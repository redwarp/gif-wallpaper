plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id "com.diffplug.spotless" version "5.12.4"
    id "com.github.ben-manes.versions" version "0.38.0"
}

android {
    compileSdkVersion 30

    defaultConfig {
        applicationId "net.redwarp.gifwallpaper"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 48
        versionName "1.9.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        resConfigs "en", "fr", "ru", "de", "es"
    }

    buildFeatures {
        viewBinding true
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
    }

    sourceSets {
        main {
            resources {
                srcDirs 'src/main/resources', "$rootDir/docs/"
            }
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

spotless {
    kotlin {
        target project.fileTree(project.rootDir) {
            include '**/net/redwarp/gifwallpaper/**/*.kt'
        }

        licenseHeaderFile 'license_header.kt'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'androidx.core:core-ktx:1.5.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.5'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.5'
    implementation "androidx.constraintlayout:constraintlayout:2.0.4"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.0'
    implementation 'androidx.lifecycle:lifecycle-service:2.3.1'
    implementation 'androidx.palette:palette-ktx:1.0.0'
    implementation "dev.sasikanth:colorsheet:1.0.1"
    implementation "io.noties.markwon:core:4.6.2"
    implementation "app.redwarp.gif:decoder:0.6.0"
    implementation "app.redwarp.gif:android-drawable:0.6.0"
    implementation "androidx.preference:preference-ktx:1.1.1"
    implementation "androidx.datastore:datastore-preferences:1.0.0-beta01"

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.robolectric:robolectric:4.5.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

dependencyUpdates {
    rejectVersionIf {
        isNonStable(it.candidate.version)
    }
}
