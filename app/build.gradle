plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id "com.diffplug.spotless"
    id "se.ascp.gradle.gradle-versions-filter" version "0.1.16"
}

android {
    compileSdkVersion 32

    defaultConfig {
        applicationId "net.redwarp.gifwallpaper"
        minSdkVersion 21
        targetSdkVersion 32
        versionCode 69
        versionName "2.0.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        resConfigs "en", "fr", "ru", "de", "es", "it", "zh"
    }

    buildFeatures {
        compose true
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = "11"
        freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
    }

    sourceSets {
        main {
            resources {
                srcDirs 'src/main/resources', "$rootDir/docs/"
            }
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    composeOptions {
        kotlinCompilerExtensionVersion '1.2.0'
    }
}

spotless {
    kotlin {
        target '**/*.kt'

        licenseHeaderFile(new File(rootDir, 'assets/license_header.kt'))
    }
}

dependencies {
    var compose_version = '1.1.1'

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation(project(':markdown'))
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'androidx.core:core-ktx:1.8.0'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'
    implementation 'androidx.lifecycle:lifecycle-service:2.5.0'
    implementation 'androidx.palette:palette-ktx:1.0.0'
    implementation "com.atlassian.commonmark:commonmark:0.17.0"
    implementation "app.redwarp.gif:decoder:1.3.1"
    implementation "app.redwarp.gif:android-drawable:1.3.1"
    implementation "androidx.datastore:datastore-preferences:1.0.0"

    // Integration with activities
    implementation 'androidx.activity:activity-compose:1.5.0'
    // Compose Material Design
    implementation "androidx.compose.material:material:$compose_version"
    // Animations
    implementation "androidx.compose.animation:animation:$compose_version"
    // Tooling support (Previews, etc.)
    implementation "androidx.compose.ui:ui-tooling:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation "androidx.navigation:navigation-compose:2.5.0"

    implementation "com.google.accompanist:accompanist-systemuicontroller:0.25.0"
    implementation "com.google.accompanist:accompanist-insets:0.23.1"

    // Integration with ViewModels
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.5.0'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.robolectric:robolectric:4.8.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    // Test rules and transitive dependencies:
    androidTestImplementation("androidx.compose.ui:ui-test-junit4:$compose_version")
    // Needed for createComposeRule, but not createAndroidComposeRule:
    debugImplementation("androidx.compose.ui:ui-test-manifest:$compose_version")
    androidTestImplementation 'tools.fastlane:screengrab:2.1.1'
}

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

dependencyUpdates {
    rejectVersionIf {
        isNonStable(it.candidate.version)
    }
}
